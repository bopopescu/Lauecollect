' This program operates the LCLS combined X-ray shutter and attenuator

' Friedrich Schotte, 12 Oct 2013 - 13 Jan 2016 

' When the level of input 4 is TTL high the shutter in open position.
' If it is low in closed or attenuated position.
' When level at input 5 is TTL high the attenuator is inserted.
' Otherwise, it is in closed position.
' Input 4 high overrides input 5 high.

DECLARATIONS
  GLOBAL Version AS STRING = "1.4"
  GLOBAL open_pos,closed_pos,attenuated_pos AS DOUBLE
  GLOBAL open_close_speed,pulsed_speed AS DOUBLE 
END DECLARATIONS

PROGRAM
  'Initialize global variables
  
  DIM bits AS INTEGER ' axis status bits
  DIM home_cyle_complete AS INTEGER ' axis status bits
  DIM current_pos AS INTEGER
  DIM open_level,att_level AS INTEGER ' digital input states
 
  SETPARM msShut_ext, DefaultRampRate, 500000 ' in deg/s2
  attenuated_pos = 56.0
  closed_pos = 63.0 ' normal closed position in open/close mode in deg
  open_pos = 70.0 ' in deg
  ' alternating closed position used only in pulsed mode in steps

  ' Timing for pulsed open mode
  pulsed_speed = 7200 ' top speed in deg/s

  ' Timing for open/close mode
  open_close_speed = 7200 ' top speed in deg/s

  FAULTACK msShut_ext ' Make sure fault state is cleared
  ENABLE msShut_ext ' turn the drive on

  ' With and incremental encoder, after power on, in order for the controller
  ' to know the absolute angle of the motor it needs to find the "reference" mark 
  ' of the encoder. The HOME command rotates the motor until the the marker input
  ' open_level goes high, then stops there and resets the encoder accumulator count to
  ' zero.
  ' The program check first if a home run has already been performed, and does
  ' it only if it has not been done before.
  bits = AXISSTATUS(msShut_ext)
  home_cyle_complete = (bits >> 1) BAND 1
  IF home_cyle_complete = 0 THEN
    HOME msShut_ext
  END IF

  WAIT MODE NOWAIT ' Set wait mode to no wait.
  ABS ' use absolute positioning mode in LINEAR command
  RAMP MODE DIST ' Set acceleration/deceleration mode to distance based.

  ' Start the loop for repetitive motion.
  WHILE 1
    ' Read digital inputs (on AUX I/O connector)
    open_level = DIN(msShut_ext,0,4) 'Close/open input (0 = closed, 1 = open)
    att_level = DIN(msShut_ext,0,5) 'Annuator input (0 = closed, 1 = attenuated)
	
	current_pos = PCMD(msShut_ext)
    IF open_level = 1 THEN
	  IF current_pos != open_pos THEN
        LINEAR msShut_ext open_pos F open_close_speed 
	  END IF
    ELSEIF att_level = 1 THEN
	  IF current_pos != attenuated_pos THEN
        LINEAR msShut_ext attenuated_pos F open_close_speed 
	  END IF
    ELSE
	  IF current_pos != closed_pos THEN
        LINEAR msShut_ext closed_pos F open_close_speed 
	  END IF
    END IF

  WEND
END PROGRAM
